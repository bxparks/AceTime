#!/usr/bin/gawk -f
#
# Usage: generate_table.sh < ${board}.out
#
# Takes the file generated by collect.sh and generates an ASCII
# table that can be inserted into the README.md.
#

BEGIN {
    labels[0] = "Baseline"
    labels[1] = "LocalDateTime"
    labels[2] = "ZonedDateTime"
    labels[3] = "Basic TimeZone (1 zone)"
    labels[4] = "Basic TimeZone (2 zones)"
    labels[5] = "Basic ZoneManager (1 zone)"
    labels[6] = "Basic ZoneManager (all)"
    labels[7] = "Extended TimeZone (1 zone)"
    labels[8] = "Extended TimeZone (2 zones)"
    labels[9] = "Extended ZoneManager (1 zone)"
    labels[10] = "Extended ZoneManager (all)"
    labels[11] = "SystemClock"
    labels[12] = "SystemClock+Basic TimeZone"
    labels[13] = "SystemClock+Extended TimeZone"
}
{
    feature = NR-1
    u[feature]["flash"] = $2
    u[feature]["ram"] = $4
}
END {
    base_flash = u[0]["flash"]
    base_ram = u[0]["ram"]
    for (i = 0; i < NR; i++) {
        flash = u[i]["flash"]
        ram = u[i]["ram"]
        u[i]["d_flash"] = flash - base_flash
        u[i]["d_ram"] = ram - base_ram
    }

    printf("+--------------------------------------------------------------+\n")
    printf("| Functionality                   |    flash/ram |       Delta |\n")
    printf("|---------------------------------+--------------+-------------|\n")
    printf("| %-31s | %6d/%5d | %5d/%5d |\n",
        labels[0], u[0]["flash"], u[0]["ram"], u[0]["d_flash"], u[0]["d_ram"])
    printf("|---------------------------------+--------------+-------------|\n")
    for (i = 1; i <= 10; i++) {
        printf("| %-31s | %6d/%5d | %5d/%5d |\n",
            labels[i], u[i]["flash"], u[i]["ram"], u[i]["d_flash"],
            u[i]["d_ram"])
    }
    printf("|---------------------------------+--------------+-------------|\n")
    for (i = 11; i <= 13; i++) {
        printf("| %-31s | %6d/%5d | %5d/%5d |\n",
            labels[i], u[i]["flash"], u[i]["ram"], u[i]["d_flash"],
            u[i]["d_ram"])
    }
    printf("+--------------------------------------------------------------+\n")
}
