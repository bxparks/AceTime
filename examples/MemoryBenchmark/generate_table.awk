#!/usr/bin/gawk -f
#
# Usage: generate_table.sh < ${board}.txt
#
# Takes the file generated by collect.sh and generates an ASCII
# table that can be inserted into the README.md.

BEGIN {
  NUM_FEATURES = 15
  labels[0] = "baseline"
  labels[1] = "LocalDateTime"
  labels[2] = "ZonedDateTime"
  labels[3] = "Manual ZoneManager"
  labels[4] = "Basic TimeZone (1 zone)"
  labels[5] = "Basic TimeZone (2 zones)"
  labels[6] = "BasicZoneManager (1 zone)"
  labels[7] = "BasicZoneManager (all zones)"
  labels[8] = "BasicZoneManager (all zones+links)"
  labels[9] = "BasicLinkManager (all links)"
  labels[10] = "Extended TimeZone (1 zone)"
  labels[11] = "Extended TimeZone (2 zones)"
  labels[12] = "ExtendedZoneManager (1 zone)"
  labels[13] = "ExtendedZoneManager (all zones)"
  labels[14] = "ExtendedZoneManager (all zones+links)"
  labels[15] = "ExtendedLinkManager (all links)"
  record_index = 0
}
{
  u[record_index]["flash"] = $2
  u[record_index]["ram"] = $4
  record_index++
}
END {
  base_flash = u[0]["flash"]
  base_ram = u[0]["ram"]
  for (i = 0; i <= NUM_FEATURES; i++) {
    if (u[i]["flash"] == -1) {
      u[i]["d_flash"] = -1
      u[i]["d_ram"] = -1
    } else {
      u[i]["d_flash"] = u[i]["flash"] - base_flash
      u[i]["d_ram"] = u[i]["ram"]- base_ram
    }
  }

  printf(\
    "+---------------------------------------------------------------------+\n")
  printf(\
    "| Functionality                          |  flash/  ram |       delta |\n")

  for (i = 0; i <= NUM_FEATURES; i++) {
    name = labels[i]
    if (name ~ /^baseline/ \
        || name ~ /^LocalDateTime/ \
        || name ~ /^Basic TimeZone \(1 zone\)/ \
        || name ~ /^Extended TimeZone \(1 zone\)/) {
      printf(\
    "|----------------------------------------+--------------+-------------|\n")
    }

    printf("| %-38s | %6d/%5d | %5d/%5d |\n",
        name, u[i]["flash"], u[i]["ram"], u[i]["d_flash"], u[i]["d_ram"])
  }
  printf(\
    "+---------------------------------------------------------------------+\n")
}
